#include "MozziConfigValues.h"

#define MOZZI_AUDIO_MODE   MOZZI_OUTPUT_I2S_DAC
#define MOZZI_I2S_PIN_BCK  26
#define MOZZI_I2S_PIN_WS   25
#define MOZZI_I2S_PIN_DATA 22
#define MOZZI_CONTROL_RATE 256

#include <Mozzi.h>
#include <Arduino.h>
#include <WiFi.h>
#include "Esp32SynchronizationContext.h"
#include "Keyboard.h"
#include "Accordion.h"

#define LED_PIN            2

Esp32SynchronizationContext syncContext;
bool updateRequested = false;

// Declare pointers for Keyboard and Accordion
Keyboard keyboard;
Accordion accordion;

void ledUpdate() {
    if (accordion.isPlaying()) {
        digitalWrite(LED_PIN, HIGH);
    } else {
        digitalWrite(LED_PIN, LOW);
    }
}

void onKeyPress(int key) {
    byte note = key + 60;
    accordion.noteOn(note);
}

void onKeyRelease(int key) {
    byte note = key + 60;
    accordion.noteOff(note);
}

void updateControl() {
    if (!syncContext.update()) {
        Serial.println("Could not update synchronization context");
    }
    
    if (updateRequested) {
        keyboard.update();
        updateRequested = false;
    }
    accordion.update();
    ledUpdate();
}

AudioOutput updateAudio() {
    float volume = 1.05f;
    int sample = accordion.nextSample() * volume;
    return MonoOutput::fromNBit(24, sample);
}

void updateKeyboardTask(void *state) {
    // RUNS ON OTHER CORE
    while (true) {
        if (updateRequested) {
            delay(1); // Feed watchdog
            continue; // Don't do anything if the main thread is still processing the last update
        }
        
        // Request the main thread to update keyboard states
        syncContext.send(
            [](void *state) {
                // RUNS ON MAIN CORE
                updateRequested = true;
            }
        );
        delay(10); // Feed watchdog
    }
}

void setup() {
    Serial.begin(115200);

    WiFi.mode(WIFI_OFF); // Disable WiFi to conserve power for audio and touch updates

    // LED debug
    pinMode(LED_PIN, OUTPUT);

    // Initialize the Accordion object
    accordion.init();
    
    // Initialize the Keyboard object and set the callbacks
    keyboard.init();
    keyboard.onKeyPress(onKeyPress);
    keyboard.onKeyRelease(onKeyRelease);

    if (!syncContext.begin()) {
        Serial.println("Error initializing synchronization context");
        while (true) {
            ; // Halt
        }
    }
    Serial.println("Synchronization context initialized.");

    // Create a task on the first core to asynchronously update touch states
    xTaskCreatePinnedToCore(
        updateKeyboardTask,  // Function that should be called
        "Keyboard Updater",  // Name of the task (for debugging)
        1000,               // Stack size (bytes)
        NULL,               // Parameter to pass
        1,                  // Task priority
        NULL,               // Task handle
        0                   // Core
    );

    startMozzi(MOZZI_CONTROL_RATE);
}

void loop() {
    audioHook();
}
